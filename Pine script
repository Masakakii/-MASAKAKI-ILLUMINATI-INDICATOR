//@version=4
study("! MASAKAKI ILLUMINATI INDICATOR", overlay=true)
 
// Moving Averages to get some example trades generated
eg1 = ema(close, 5)
eg2 = ema(close, 13)
 
long = crossover(eg1, eg2)
short = crossunder(eg1, eg2)
 
//plot signals on MA cross
plotshape(long, style=shape.labelup, location=location.belowbar, color=color.green, size=size.tiny, title="Long Entry", text="LONG", textcolor=color.white)
plotshape(short, style=shape.labeldown, location=location.abovebar, color=color.red, size=size.tiny, title="Short Entry", text="SHORT", textcolor=color.white)
 
///////////////
/////////
// The Fixed Percent Stop Loss Code
// User Options to Change Inputs (%)
stopPer = input(3, title='Stop Loss %', type=input.float) / 100
takePer = input(9, title='Take Profit %', type=input.float) / 100
 
//detect what was last signal (long or short)
long_short = 0
long_last = long and (nz(long_short[1]) == 0 or nz(long_short[1]) == -1)
short_last = short and (nz(long_short[1]) == 0 or nz(long_short[1]) == 1)
long_short := long_last ? 1 : short_last ? -1 : long_short[1]
 
//entry price
longPrice = valuewhen(long_last, close, 0)
shortPrice = valuewhen(short_last, close, 0)
//fixed sltp prices
longStop = longPrice * (1 - stopPer)
shortStop = shortPrice * (1 + stopPer)
longTake = longPrice * (1 + takePer)
shortTake = shortPrice * (1 - takePer)
//plot sltp lines
plot(long_short==1  ? longStop : na, style=plot.style_linebr, color=color.red, linewidth=1, title="Long Fixed SL")
plot(long_short==-1 ? shortStop : na, style=plot.style_linebr, color=color.red, linewidth=1, title="Short Fixed SL")
plot(long_short==1  ? longTake : na, style=plot.style_linebr, color=color.green, linewidth=1, title="Long Fixed TP")
plot(long_short==-1 ? shortTake : na, style=plot.style_linebr, color=color.green, linewidth=1, title="Short Fixed TP")
 
//remove first bar for SL/TP (you can't enter a trade at bar close THEN hit your SL on that same bar)
longBar1 = barssince(long_last)
longBar2 = longBar1 >= 1 ? true : false
shortBar1 = barssince(short_last)
shortBar2 = shortBar1 >= 1 ? true : false
 
//check for SL hit during a bar
longSLhit = long_short==1 and longBar2 and low < longStop
plotshape(longSLhit, style=shape.labelup, location=location.belowbar, color=color.gray, size=size.tiny, title="Long SL", text=" Long SL", textcolor=color.white)
shortSLhit = long_short==-1 and shortBar2 and high > shortStop
plotshape(shortSLhit, style=shape.labeldown, location=location.abovebar, color=color.gray, size=size.tiny, title="Short SL", text=" Short SL", textcolor=color.white)
 
//check for TP hit during bar
longTPhit = long_short==1 and longBar2 and high > longTake
plotshape(longTPhit, style=shape.labeldown, location=location.abovebar, color=color.purple, size=size.tiny, title="Long TP", text="Long TP", textcolor=color.white)
shortTPhit = long_short==-1 and shortBar2 and low < shortTake
plotshape(shortTPhit, style=shape.labelup, location=location.belowbar, color=color.purple, size=size.tiny, title="Short TP", text="Short TP", textcolor=color.white)
 
//reset long_short if SL/TP hit during bar
long_short := (long_short==1 or long_short==0) and longBar2 and (longSLhit or longTPhit) ? 0 : (long_short==-1 or long_short==0) and shortBar2 and (shortSLhit or shortTPhit) ? 0 : long_short
//
 
//set Alerts for Open Long/Short, SL Hit, TP Hit
alertcondition(condition=long_last, title="Long Alarm", message='Open a Long Trade @ ${{close}}')
alertcondition(condition=short_last, title="Short Alarm", message='Open a Short Trade @ ${{close}}')
 
alertcondition(condition=longSLhit, title="Long Stop", message='Long SL Hit @ ${{plot("Long SL")}}')
alertcondition(condition=shortSLhit, title="Short Stop", message='Short SL Hit @ ${{plot("Short SL")}}')
 
alertcondition(condition=longTPhit, title="Long Take Profit", message='Long TP Hit @ ${{plot("Long TP")}}')
alertcondition(condition=shortTPhit, title="Short Take Profit", message='Short TP Hit @ ${{plot("Short TP")}}')
//
